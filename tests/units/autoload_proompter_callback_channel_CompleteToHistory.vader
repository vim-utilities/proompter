
""
" {{{
Execute (proompter#callback#channel#CompleteToHistory -- Handles well formated HTTP response without images):
  let separator = "\r\n"

  let api_response = join([
        \   'HTTP/1.1 200 OK',
        \   'Server: SimpleHTTP/0.6 Python/3.12.6',
        \   'Date: Sat, 28 Sep 2024 23:29:00 GMT',
        \   'Content-Type: application/json',
        \   '',
        \   json_encode({
        \     'model': 'mistral',
        \     'created_at': '2023-11-03T15:36:02.583064Z',
        \     'response': ' The sky appears blue because of a phenomenon called Rayleigh scattering.',
        \     'done': v:true,
        \     'done_reason': 'stop',
        \     'total_duration': 8493852375,
        \     'load_duration': 6589624375,
        \     'prompt_eval_count': 14,
        \     'prompt_eval_duration': 119039000,
        \     'eval_count': 110,
        \     'eval_duration': 1779061000,
        \   }),
        \ ], separator)

  let configurations = {
        \   'select': {
        \     'model_name': 'mistral',
        \   },
        \ }

  let expected_configurations = deepcopy(configurations)

  let state = { 'messages': [] }

  let expected_state = {
        \   'messages': [
        \     {
        \       'model': 'mistral',
        \       'created_at': '2023-11-03T15:36:02.583064Z',
        \       'done': v:true,
        \       'done_reason': 'stop',
        \       'context': v:null,
        \       'message': {
        \         'role': 'assistant',
        \         'content': ' The sky appears blue because of a phenomenon called Rayleigh scattering.',
        \         'images': v:null,
        \         'tool_calls': v:null,
        \       },
        \     },
        \   ],
        \ }

  call proompter#callback#channel#CompleteToHistory(api_response, configurations, state)

  AssertEqual state, expected_state
  AssertEqual configurations, expected_configurations, 'Unexpected mutation of configurations'

Execute (proompter#callback#channel#CompleteToHistory -- Throws when status code is not in 200 range):
  let separator = "\r\n"

  let api_response = join([
        \   'HTTP/1.1 500 Internal Server Error',
        \   'Server: SimpleHTTP/0.6 Python/3.12.6',
        \ ], separator)

  let configurations = {
        \   'select': {
        \     'model_name': 'mistral',
        \   },
        \ }

  let expected_configurations = deepcopy(configurations)

  let state = { 'messages': [] }

  let expected_state = { 'messages': [ ] }

  AssertThrows call proompter#callback#channel#CompleteToHistory(g:api_response, g:configurations, g:state)

  AssertEqual g:vader_exception, 'HTTP response not okay -> 500 Internal Server Error'
  AssertEqual state, expected_state, 'Unexpected mutation of state'
  AssertEqual configurations, expected_configurations, 'Unexpected mutation of configurations'
" }}}
""

